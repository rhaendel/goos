sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.integrationTest.output
    }
    endtoendTest {
        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.integrationTest.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.integrationTest.output
        runtimeClasspath += sourceSets.endtoendTest.output
    }
    endtoendFailingTest {
        compileClasspath += sourceSets.endtoendTest.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.endtoendTest.output
        runtimeClasspath += sourceSets.endtoendFailingTest.output
    }
}

dependencies {
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime
    endtoendTestCompile configurations.integrationTestCompile
    endtoendTestRuntime configurations.integrationTestRuntime
    endtoendFailingTestCompile configurations.endtoendTestCompile
    endtoendFailingTestRuntime configurations.endtoendTestRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
    reports.html.destination = file("$buildDir/reports/integrationTest")
}

task endtoendTest(type: Test) {
    testClassesDir = sourceSets.endtoendTest.output.classesDir
    classpath = sourceSets.endtoendTest.runtimeClasspath
    mustRunAfter integrationTest
    reports.html.destination = file("$buildDir/reports/endtoendTest")
}

task endtoendFailingTest(type: Test) {
    testClassesDir = sourceSets.endtoendFailingTest.output.classesDir
    classpath = sourceSets.endtoendFailingTest.runtimeClasspath
    mustRunAfter endtoendTest
    reports.html.destination = file("$buildDir/reports/endtoendFailingTest")
}

check.dependsOn integrationTest
check.dependsOn endtoendTest
check.dependsOn endtoendFailingTestClasses

//
// eclipse plugin needs some extra configuration:
//
configurations{
    projectInternalDependencies
}

dependencies {
    projectInternalDependencies sourceSets.main.output
}

eclipse {
    classpath {
        plusConfigurations += [ configurations.endtoendFailingTestCompile, configurations.endtoendFailingTestRuntime ]
        minusConfigurations += [ configurations.projectInternalDependencies ]
    }
}
